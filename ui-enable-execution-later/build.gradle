import java.nio.file.Files
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.7"
        classpath "com.moowork.gradle:gradle-node-plugin:1.3.1"

    }
}

apply plugin:"asset-pipeline"
apply plugin: 'maven'


ext.pluginName = 'Execution Mode Later UI Plugin'
ext.pluginDescription = 'Enable/Disable execution/schedule later.'
ext.sopsCopyright = "Â© 2019, Rundeck, Inc."
ext.sopsUrl = "http://rundeck.com"
ext.buildDateString=new Date().format("yyyy-MM-dd'T'HH:mm:ssX")
ext.archivesBaseName = "ui-execution-mode-later"
ext.pluginBaseFolder = "."

ext.archiveFilename = ext.archivesBaseName + '-' + scmVersion.version

assets {
    verbose = true
    minifyJs = false
    minifyCss = false
    enableSourceMaps = false
    enableGzip = false
    configOptions = [:]

    minifyOptions = [
            languageMode     : 'ES5',
            targetLanguage   : 'ES5', //Can go from ES6 to ES5 for those bleeding edgers
            optimizationLevel: 'SIMPLE',
            angularPass      : false // Can use @ngInject annotation for Angular Apps
    ]

    excludes = [
            '**/*.less',
            '**/*.html',
            'js/lib/*.js',
            'js/lib/**/*.js'
    ]

    //for plugin packaging
    packagePlugin = false //set to true if this is a library

    //developmentRuntime can be turned off
    developmentRuntime = true

    //if you want to customize the jar task this task runs on you can specify a jarTaskName
    jarTaskName = null

    // Can add custom asset locations (directories or individual jar files)
    from "${project.projectDir}/src/main/rdplugin/assets"
    compileDir = "${project.buildDir}/assets/resources"
}


task buildZip(type: Jar, group: 'build') {

    destinationDir = file("build/distributions")
    baseName = project.ext.archivesBaseName
    version = scmVersion.version
    extension = 'zip'

    from("${project.buildDir}/zip-contents") {
        into(archiveFilename)
        exclude "resources/manifest.properties"
    }

    manifest {
        attributes 'Rundeck-Plugin-Name': pluginName.toString(),
                   'Rundeck-Plugin-Description': pluginDescription.toString(),
                   'Rundeck-Plugin-Archive': 'true',
                   'Rundeck-Plugin-File-Version': scmVersion.version,
                   'Rundeck-Plugin-Author': sopsCopyright,
                   'Rundeck-Plugin-URL': sopsUrl,
                   'Rundeck-Plugin-Date': buildDateString
    }

}

buildZip.doFirst {
    def assetsDir = "${project.buildDir}/assets/resources"
    def assetsMap = new Properties()
    def assetsManifest = file("${assetsDir}/manifest.properties")
    assetsManifest.withInputStream(assetsMap.&load)
    def tokens = assetsMap + [
            version    : project.version,
            date       : new Date().toString(),
            author     : sopsCopyright,
            url        : sopsUrl,
            title      : pluginName,
            description: pluginDescription,
            name       : archivesBaseName.toString(),
    ]
    copy {
        //load asset mapping
        from(assetsDir) {
            into 'resources'
        }
        from("${project.projectDir}/src/main/rdplugin") {
            filter(ReplaceTokens, tokens: tokens)
            exclude "assets"
        }
        into "${project.buildDir}/zip-contents"
    }
}

project.buildZip.dependsOn 'assetCompile'

artifacts {
    archives file: file("build/distributions"), name: "${project.name}", type: 'directory', builtBy: buildZip
}
